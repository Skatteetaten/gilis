buildscript {
  ext.repos = {
    mavenCentral()
    mavenLocal()
  }

  repositories repos

  dependencies {
    classpath(
        'no.skatteetaten.aurora.gradle.plugins:aurora-gradle-plugin:1.3.0'
    )
  }
}

plugins {
  id "org.jetbrains.kotlin.jvm" version "1.3.11"
  id "org.jetbrains.kotlin.plugin.spring" version "1.3.11"
  id 'org.springframework.boot' version '2.1.1.RELEASE'
  id 'org.jlleitschuh.gradle.ktlint' version '6.3.1'
  id 'com.github.ben-manes.versions' version '0.20.0'
  id "com.gorylenko.gradle-git-properties" version "1.4.17"
}

ext.aurora = [
    requireStaging          : false,
    applySpockSupport       : false,
    setProjectVersionFromGit: false
]

apply plugin: 'io.spring.dependency-management'
apply plugin: 'no.skatteetaten.plugins.aurora'

repositories repos

[jar, distZip]*.enabled = true
[bootJar, distTar, bootDistTar, bootDistZip]*.enabled = false
configurations.archives.artifacts.removeIf { !it.archiveTask.enabled }

group = groupId

version "${version}"

def junitJupiterVersion = dependencyManagement.importedProperties['junit-jupiter.version']
dependencies {

  compile(
      'org.jetbrains.kotlin:kotlin-reflect',
      'org.jetbrains.kotlin:kotlin-stdlib-jdk8',
      'com.fasterxml.jackson.module:jackson-module-kotlin',
      'com.fasterxml.jackson.datatype:jackson-datatype-jsr310',
      'no.skatteetaten.aurora.springboot:aurora-spring-boot2-starter:1.4.0',
      'io.fabric8:openshift-client:4.1.0',
      'com.fkorotkov:kubernetes-dsl:1.2.1',
      'org.springframework.boot:spring-boot-starter-security',
      'org.springframework.boot:spring-boot-starter-webflux',
  )

  [
      'org.junit.jupiter:junit-jupiter-api',
      "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}",
      'org.springframework.boot:spring-boot-starter-test',
      'io.mockk:mockk:1.8.9',
      'com.willowtreeapps.assertk:assertk:0.10',
  ].each { testCompile(it) { exclude group: 'junit' } }

  testImplementation(
      "org.junit.jupiter:junit-jupiter-api",
      "com.squareup.okhttp3:mockwebserver:3.10.0"
  )
  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
}

springBoot {
  buildInfo()
}

compileKotlin {
  kotlinOptions {
    suppressWarnings = true
    jvmTarget = 1.8
    freeCompilerArgs = ["-Xjsr305=strict"]
  }
}

compileTestKotlin {
  kotlinOptions {
    suppressWarnings = true
    jvmTarget = 1.8
  }
}

test {
  useJUnitPlatform()
}
